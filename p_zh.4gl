# Prog. Version..: '5.30.24-17.04.13(00009)'     #
#
# Pattern name...: p_zh.4gl
# Descriptions...: 程式檔案歷程維護作業
# Date & Author..: 03/08/22 By Letter
# Modify.........: No.MOD-470041 04/07/19 By Wiky 修改INSERT INTO...
# Modify.........: No.FUN-660081 06/06/15 By Carrier cl_err-->cl_err3
# Modify.........: No.FUN-680135 06/08/31 By Hellen  欄位類型修改
# Modify.........: No.FUN-6A0096 06/10/26 By johnray l_time轉g_time
# Modify.........: No.FUN-6A0092 06/11/14 By Jackho 新增動態切換單頭隱藏的功能
# Modify.........: No.TQC-740155 07/04/26 By kim zz02以gaz03取代
# Modify.........: No.TQC-860017 08/06/09 By Jerry 修改程式控制區間內,缺乏ON IDLE的部份
# Modify.........: No.FUN-980030 09/08/31 By Hiko 加上GP5.2的相關設定
# Modify.........: No.FUN-9A0024 09/10/13 By destiny display xxx.*改為display對應欄位
# Modify.........: No:FUN-D30034 13/04/18 By xumm 修改單身新增時按下放棄鍵未執行AFTER INSERT的問題
# Modify.........: No:MOD-EC0025 14/12/04 By ycchao 修改單身新增時用tab鍵換行會多出一行空白
# Modify.........: No:MOD-EC0080 14/12/15 By ycchoa 修改單身顯示資料時會多出一行空白
# Moidfy.........: NO:26030018   20260312 By momo 改版為適合填寫之格式
 
DATABASE ds
 
GLOBALS "../../../tiptop/config/top.global"
 
#模組變數(Module Variables)
DEFINE
    g_gam           RECORD LIKE gam_file.*,     #程式歷程維護作業(單頭)
    g_gam_t         RECORD LIKE gam_file.*,     #程式歷程維護作業(舊值)
    g_gam_o         RECORD LIKE gam_file.*,     #程式歷程維護作業(舊值)
    g_gam01_t         LIKE gam_file.gam01,      #程式代號(舊值)
    g_gaz03           LIKE gaz_file.gaz03,        #(程式名稱) #TQC-740155
    g_gan        DYNAMIC ARRAY of RECORD        #程式變數-單身(Program Variables)
        gam01         LIKE gam_file.gam01,      #需求單號
        gam02         LIKE gam_file.gam02,      #程式代號
        gan01         LIKE gan_file.gan01,      #項次
        gan03         LIKE gan_file.gan03,      #說明
        ganmodu       LIKE gan_file.ganmodu,    #修改人
        gandate       LIKE gan_file.gandate     #時間
    END RECORD,
    g_gan_t         RECORD                      #程式變數-單身(Program Variables)
        gam01         LIKE gam_file.gam01,      #需求單號
        gam02         LIKE gam_file.gam02,      #程式代號
        gan01         LIKE gan_file.gan01,      #項次
        gan03         LIKE gan_file.gan03,      #說明
        ganmodu       LIKE gan_file.ganmodu,    #修改人
        gandate       LIKE gan_file.gandate     #時間
    END RECORD,
    g_gan01_t           LIKE gan_file.gan01,    #(舊值)
    g_wc,g_wc2,g_sql    string,                 #No.FUN-580092 HCN
    g_cmd               LIKE type_file.chr1000, #No.FUN-680135 VARCHAR(200)
    g_rec_b             LIKE type_file.num5,    #單身筆數              #No.FUN-680135 SMALLINT
    l_ac                LIKE type_file.num5,    #目前處理的ARRAY CNT   #No.FUN-680135 SMALLINT
    l_sl                LIKE type_file.num5,    #目前處理的SCREEN LINE #No.FUN-680135 SMALLINT
    g_argv1             LIKE gam_file.gam01     #程式代號
DEFINE   g_curs_index   LIKE type_file.num10   #No.FUN-680135 INTEGER
DEFINE   g_row_count    LIKE type_file.num10   #No.FUN-580092 HCN     #No.FUN-680135 INTEGER
DEFINE   g_jump         LIKE type_file.num10,  #No.FUN-680135 INTEGER
         g_no_ask       LIKE type_file.num5    #No.FUN-680135 SMALLINT
DEFINE   g_cnt           LIKE type_file.num10   #No.FUN-680135 INTEGER
DEFINE   g_log           LIKE type_file.chr1    #No.FUN-680135 VARCHAR(1)
DEFINE   g_msg           LIKE type_file.chr1000 #No.FUN-680135 VARCHAR(72)
DEFINE   g_forupd_sql    STRING
DEFINE   g_before_input_done  LIKE type_file.num5   #No.FUN-680135 SMALLINT
 
MAIN
    OPTIONS                                     #改變一些系統預設值
        INPUT NO WRAP
    DEFER INTERRUPT                             #擷取中斷鍵, 由程式處理
 
    LET g_argv1 =ARG_VAL(1)
 
    IF (NOT cl_user()) THEN
       EXIT PROGRAM
    END IF
 
    WHENEVER ERROR CALL cl_err_msg_log
 
    IF (NOT cl_setup("AZZ")) THEN
       EXIT PROGRAM
    END IF
 
    CALL cl_used(g_prog,g_time,1) RETURNING g_time    #No.FUN-6A0096
 
    LET g_forupd_sql = "SELECT gam01,gam02 FROM gam_file  WHERE gam01 = ? ",
                         " FOR UPDATE "     # g_gam.gam01
    LET g_forupd_sql = cl_forupd_sql(g_forupd_sql)
    DECLARE zh_cl CURSOR FROM g_forupd_sql
 
    OPEN WINDOW zh_w WITH FORM "czz/42f/p_zh"
    ATTRIBUTE(STYLE=g_win_style CLIPPED)
 
    CALL cl_ui_init()
 
    IF NOT cl_null(g_argv1) THEN
       CALL zh_q()
       CALL zh_u()
       LET g_argv1=NULL
    END IF
 
    CALL zh_menu()
 
    CLOSE WINDOW zh_w                         #結束畫面
 
    CALL cl_used(g_prog,g_time,2) RETURNING g_time
END MAIN
 
 
 
#QBE 查詢資料
FUNCTION zh_cs()
    CLEAR FORM                               #清除畫面
    CALL cl_set_head_visible("","YES")       #No.FUN-6A0092
 
     CONSTRUCT g_wc ON                       # 螢幕上取條件
        gam01,gam02,gan03,ganmodu,gandate
        FROM s_gan[1].gam01,s_gan[1].gam02,s_gan[1].gan03,s_gan[1].ganmodu,s_gan[1].gandate

        BEFORE CONSTRUCT
                 CALL cl_qbe_init()
 
        ON ACTION about
           CALL cl_about()
 
        ON ACTION controlg
           CALL cl_cmdask()
 
        ON ACTION help
           CALL cl_show_help()
 
        ON IDLE g_idle_seconds
           CALL cl_on_idle()
           CONTINUE CONSTRUCT
     END CONSTRUCT
 
    IF INT_FLAG THEN RETURN END IF

    #資料權限的檢查
        IF g_priv2='4' THEN                           #只能使用自己的資料
            LET g_wc = g_wc clipped," AND gamuser = '",g_user,"'"
        END IF
        IF g_priv3='4' THEN                           #只能使用相同群的資料
            LET g_wc = g_wc clipped," AND gamgrup MATCHES '",g_grup CLIPPED,"*'"
        END IF
 
        IF g_priv3 MATCHES "[5678]" THEN    #TQC-5C0134群組權限
            LET g_wc = g_wc clipped," AND gamgrup IN ",cl_chk_tgrup_list()
        END IF
    LET g_wc = g_wc CLIPPED,cl_get_extra_cond('gamuser', 'gamgrup')
 
 
    LET g_sql = "SELECT gam01,gam02,gan01,gan03,ganmodu,gandate ",
                "FROM gan_file,gam_file ",
                " WHERE gam01=gan02 AND ", g_wc CLIPPED,
                " ORDER BY gam01"
 
    PREPARE gam_prepare FROM g_sql
    DECLARE gam_cs                         #SCROLL CURSOR
        SCROLL CURSOR WITH HOLD FOR gam_prepare
 
    LET g_sql="SELECT COUNT(*) FROM gam_file,gan_file WHERE gam01=gan02 AND ",g_wc CLIPPED
 
    PREPARE gam_precount FROM g_sql
    DECLARE gam_count CURSOR FOR gam_precount
 
END FUNCTION
 
#中文的MENU
FUNCTION zh_menu()
 
   WHILE TRUE
      CALL zh_bp("G")
      CASE g_action_choice
 
        WHEN "query"
            IF cl_chk_act_auth() THEN
                CALL zh_q()
            END IF

        WHEN "exporttoexcel" 
            IF cl_chk_act_auth() THEN
              CALL cl_export_to_excel(ui.Interface.getRootNode(),base.TypeInfo.create(g_gan),'','')
            END IF
 
        WHEN "detail"
            IF cl_chk_act_auth() THEN
                CALL zh_b()
            ELSE
               LET g_action_choice = NULL
            END IF

        WHEN "related_document"  #相關文件
              IF cl_chk_act_auth() THEN
                 IF g_gan[l_ac].gam01 IS NOT NULL THEN
                 LET g_doc.column1 = "gan02"
                 LET g_doc.value1 = g_gan[l_ac].gam01
                 CALL cl_doc()
               END IF
         END IF
 
        WHEN "help"
            CALL cl_show_help()
 
        WHEN "exit"
            EXIT WHILE
 
        WHEN "controlg"
            CALL cl_cmdask()
 
      END CASE
   END WHILE
 
END FUNCTION
 
 
#Add  輸入
FUNCTION zh_a()
    IF s_shut(0) THEN RETURN END IF
    MESSAGE ""
    CLEAR FORM
    CALL g_gan.clear()
    INITIALIZE g_gam.* LIKE gam_file.*             #DEFAULT 設定
    LET g_gam01_t = NULL
    #預設值及將數值類變數清成零
    LET g_gam_o.* = g_gam.*
    CALL cl_opmsg('a')
    WHILE TRUE
        LET g_gam.gam01 = g_gan[g_cnt].gam01
        LET g_gam.gam02 = g_gan[g_cnt].gam02
        LET g_gam.gamuser=g_user
        LET g_gam.gamgrup=g_grup
        LET g_gam.gammodu=g_grup
        LET g_gam.gamdate=g_today
        LET g_gam.gamoriu = g_user     
        LET g_gam.gamorig = g_grup  
        #CALL zh_i("a")                #輸入單頭
        IF INT_FLAG THEN                   #使用者不玩了
            INITIALIZE g_gam.* TO NULL
            LET INT_FLAG = 0
            CALL cl_err('',9001,0)
            EXIT WHILE
        END IF
        IF g_gam.gam01 IS NULL THEN                # KEY 不可空白
            CONTINUE WHILE
        END IF
        INSERT INTO gam_file VALUES (g_gam.*)
        IF SQLCA.sqlcode THEN   			#置入資料庫不成功
            CALL cl_err3("ins","gam_file",g_gam.gam01,"",SQLCA.sqlcode,"","",1)    #No.FUN-660081
            CONTINUE WHILE
        END IF
        SELECT gam01 INTO g_gam.gam01 FROM gam_file
            WHERE gam01 = g_gam.gam01
        LET g_gam01_t = g_gam.gam01            #保留舊值
        LET g_gam_t.* = g_gam.*
 
        CALL g_gan.clear()
        LET g_rec_b = 0
        CALL zh_b()                            #輸入單身
        EXIT WHILE
    END WHILE
END FUNCTION
 
#處理INPUT
FUNCTION zh_i(p_cmd)
DEFINE
    l_flag          LIKE type_file.chr1,       #判斷必要欄位是否有輸入 #No.FUN-680135 VARCHAR(1) 
    p_cmd           LIKE type_file.chr1,       #a:輸入 u:更改          #No.FUN-680135 VARCHAR(1)
    l_n             LIKE type_file.num5        #No.FUN-680135 SMALLINT
 
    DISPLAY BY NAME g_gam.gam01,g_gam.gam02
    CALL cl_set_head_visible("","YES")       #No.FUN-6A0092
 
    INPUT BY NAME g_gam.gam01,g_gam.gam02
        WITHOUT DEFAULTS ATTRIBUTE (YELLOW) HELP 1
 
        BEFORE INPUT
           LET g_before_input_done = FALSE
           CALL p_zh_set_entry(p_cmd)
           CALL p_zh_set_no_entry(p_cmd)
           LET g_before_input_done = TRUE
 
        AFTER FIELD gam01
            IF g_gam.gam01 IS NULL THEN
               NEXT FIELD gam01
            END IF
            DISPLAY BY NAME g_gam.gam01 ATTRIBUTE(REVERSE)
 
         IF g_gam.gam01 IS NOT NULL THEN
            IF p_cmd = "a" OR                    # 若輸入或更改且改KEY
               (p_cmd = "u" AND g_gam.gam01 != g_gam01_t) THEN
               SELECT count(*) INTO l_n FROM gam_file
                  WHERE gam01 = g_gam.gam01
               IF l_n > 0 THEN                  # Duplicated
                  CALL cl_err(g_gam.gam01,-239,0)
                  LET g_gam.gam01 = g_gam01_t
                  DISPLAY BY NAME g_gam.gam01
                  NEXT FIELD gam01
               END IF
            END IF
         END IF
 
        AFTER FIELD gam02
            IF g_gam.gam02 IS NULL THEN
               NEXT FIELD gam02
            END IF
            DISPLAY BY NAME g_gam.gam02 ATTRIBUTE(REVERSE)
 
        AFTER INPUT  #判斷必要欄位之值是否有值,若無則反白顯示,並要求重新輸入
            LET l_flag='N'
            IF INT_FLAG THEN
               EXIT INPUT
            END IF
            IF cl_null(g_gam.gam02)  THEN
               LET l_flag='Y'
               DISPLAY BY NAME g_gam.gam02 ATTRIBUTE(REVERSE)
            END IF
            IF l_flag='Y' THEN
                CALL cl_err('','9033',0)
                NEXT FIELD gam02
            END IF
 
        ON KEY(CONTROL-F)                  #欄位說明
            CASE
                WHEN INFIELD(gam01) CALL cl_fldhlp('gam01')
                WHEN INFIELD(gam02) CALL cl_fldhlp('gam02')
                OTHERWISE           CALL cl_fldhlp('    ')
            END CASE
 
        ON KEY(CONTROL-O)                        # 沿用所有欄位
            IF INFIELD(gam01) THEN
                LET g_gam.* = g_gam_t.*
                #No.FUN-9A0024--begin 
                #DISPLAY BY NAME g_gam.*
                DISPLAY g_gam.gam01 TO gam01
                DISPLAY g_gam.gam02 TO gam02
                #No.FUN-9A0024--end
                 ATTRIBUTE(YELLOW)
                NEXT FIELD gam01
            END IF
 
        ON KEY(CONTROL-G)
            CALL cl_cmdask()
   
#TQC-860017 start
 
        ON ACTION about
           CALL cl_about()
 
        ON ACTION help
           CALL cl_show_help()
 
        ON IDLE g_idle_seconds
           CALL cl_on_idle()
          CONTINUE INPUT
#TQC-860017 end
    END INPUT
END FUNCTION
 
FUNCTION p_zh_set_entry(p_cmd)
   DEFINE   p_cmd   LIKE type_file.chr1          #No.FUN-680135 VARCHAR(1)
 
   IF p_cmd = 'a' AND (NOT g_before_input_done) THEN
      CALL cl_set_comp_entry("gam01",TRUE)
   END IF
END FUNCTION
 
FUNCTION p_zh_set_no_entry(p_cmd)
   DEFINE   p_cmd   LIKE type_file.chr1          #No.FUN-680135 VARCHAR(1)
 
   IF p_cmd = 'u' AND (NOT g_before_input_done) THEN
      CALL cl_set_comp_entry("gam01",FALSE)
   END IF
END FUNCTION
 
#Query 查詢
FUNCTION zh_q()
    # 2004/02/06 by Hiko : 初始化單頭資料筆數.
     LET g_row_count = 0 #No.FUN-580092 HCN
    LET g_curs_index = 0
     CALL cl_navigator_setting(g_curs_index,g_row_count) #No.FUN-580092 HCN
 
    CALL cl_opmsg('q')
    MESSAGE ""
    INITIALIZE g_gam.* TO NULL
    CLEAR FORM
    DISPLAY '   ' TO FORMONLY.cnt  #ATTRIBUTE(YELLOW)
    CALL zh_cs()
    IF INT_FLAG THEN
        LET INT_FLAG = 0
        INITIALIZE g_gam.* TO NULL
        RETURN
    END IF
    MESSAGE " SEARCHING ! " ATTRIBUTE(REVERSE)
    OPEN gam_cs                            # 從DB產生合乎條件TEMP(0-30秒)
    IF SQLCA.sqlcode THEN
        CALL cl_err('',SQLCA.sqlcode,0)
        INITIALIZE g_gam.* TO NULL
    ELSE
        OPEN gam_count
         FETCH gam_count INTO g_row_count #No.FUN-580092 HCN
         DISPLAY g_row_count TO FORMONLY.cnt  #ATTRIBUTE(MAGENTA) #No.FUN-580092 HCN
        CALL zh_fetch('F')                    # 讀出TEMP第一筆並顯示
    END IF
    MESSAGE ""
END FUNCTION
 
#處理資料的讀取
FUNCTION zh_fetch(p_flag)
DEFINE
    p_flag          LIKE type_file.chr1,      #處理方式    #No.FUN-680135 VARCHAR(1) 
    l_abso          LIKE type_file.num10      #絕對的筆數  #No.FUN-680135 INTEGER
 
    CASE p_flag
        WHEN 'N' FETCH NEXT     gam_cs INTO g_gam.gam01
        WHEN 'P' FETCH PREVIOUS gam_cs INTO g_gam.gam01
        WHEN 'F' FETCH FIRST    gam_cs INTO g_gam.gam01
        WHEN 'L' FETCH LAST     gam_cs INTO g_gam.gam01
        WHEN '/'
            IF (NOT g_no_ask) THEN
               CALL cl_getmsg('fetch',g_lang) RETURNING g_msg
               PROMPT g_msg CLIPPED,': ' FOR g_jump
                  ON IDLE g_idle_seconds
                     CALL cl_on_idle()
               END PROMPT
               IF INT_FLAG THEN
                   LET INT_FLAG = 0
                   EXIT CASE
               END IF
            END IF
            FETCH ABSOLUTE g_jump gam_cs INTO g_gam.gam01
            LET g_no_ask = FALSE
    END CASE
 
    #Letter 加入
 
     #IF g_gam.gam01 IS NULL THEN
     #   LET g_gam.gam01=g_argv1
        #SELECT zz02 INTO g_zz02
        #FROM zz_file
        #WHERE zz01=g_gam.gam01
        #CALL zh_show()
        #加入單頭資料入資料庫
        #CALL zh_i('a')
        #INSERT INTO gam_file (gam01,gam02) VALUES(g_gam.gam01,g_gam.gam02)
        #CALL zh_u()
        #CALL zh_b()
 
     #ELSE
       #TQC-740155.............begin
       #LET g_zz02 =NULL 
       #SELECT gam02,zz02 INTO g_gam.gam02,g_zz02
       #FROM gam_file,OUTER zz_file
       #WHERE gam01 = g_gam.gam01
       #AND  zz01  = gam01
        LET g_gaz03=cl_get_progdesc(g_gam.gam01,g_lang)  #g_gam.gam01
       #TQC-740155.............end
        CALL zh_show()
     #END IF
 
 
#   #暫時Mark起來
#    
#   IF SQLCA.sqlcode THEN
#       CALL cl_err(g_gam.gam01,SQLCA.sqlcode,0)
#       RETURN
#   END IF
#
#   SELECT gam02,zz02 INTO g_gam.gam02,g_zz02
#     FROM gam_file,OUTER zz_file
#    WHERE gam01 = g_gam.gam01
#     AND  zz01  = gam01
#    
#   IF SQLCA.sqlcode THEN
#       CALL cl_err(g_gam.gam01,SQLCA.sqlcode,0)
#       INITIALIZE g_gam.* TO NULL
#       RETURN
#   END IF
#   CALL zh_show()
 
END FUNCTION
 
#將資料顯示在畫面上
FUNCTION zh_show()
    LET g_gam_t.* = g_gam.*                #保存單頭舊值
    LET g_gam_o.* = g_gam.*                #保存單頭舊值
   # DISPLAY BY NAME g_gam.*                #顯示單頭值
    DISPLAY g_gam.gam01 TO gam01
    DISPLAY g_gam.gam02 TO gam02
   #DISPLAY g_zz02 TO FORMONLY.zz02 #TQC-740155
    DISPLAY g_gaz03 TO FORMONLY.gaz03 #TQC-740155
    CALL zh_b_fill(g_wc)                   #單身
    CALL cl_show_fld_cont()                   #No.FUN-550037 hmf
END FUNCTION
 
#取消整筆 (所有合乎單頭的資料)

FUNCTION zh_r()
    IF s_shut(0) THEN RETURN END IF
    IF g_gam.gam01 IS NULL THEN
        CALL cl_err("",-400,0)
        RETURN
    END IF
    BEGIN WORK
 
    OPEN zh_cl USING g_gam.gam01
    IF SQLCA.sqlcode THEN
        CALL cl_err('OPEN zh_cl',SQLCA.sqlcode,1)   #資料被他人LOCK
        RETURN
    END IF
 
    FETCH zh_cl INTO g_gam.*               # 鎖住將被更改或取消的資料
    IF SQLCA.sqlcode THEN
        CALL cl_err('FETCH zh_cl',SQLCA.sqlcode,1)   #資料被他人LOCK
        RETURN
    END IF
    CALL zh_show()
    IF cl_delh(0,0) THEN                   #確認一下
         DELETE FROM gam_file WHERE gam01 = g_gam.gam01 AND gam02 = g_gam.gam02
         DELETE FROM gan_file WHERE gan03 = g_gam.gam01 AND gan01 = g_gan_t.gan01
         INITIALIZE g_gam.* TO NULL
         CLEAR FORM
    END IF
    CLOSE zh_cl
    #IF g_log = 'Y' THEN COMMIT WORK END IF
    COMMIT WORK
END FUNCTION
  
#單身
FUNCTION zh_b()
DEFINE
 
    l_ac_t          LIKE type_file.num5,                #未取消的ARRAY CNT     #No.FUN-680135 SMALLINT 
    l_n             LIKE type_file.num5,                #檢查重複用            #No.FUN-680135 SMALLINT
    l_lock_sw       LIKE type_file.chr1,                #單身鎖住否            #No.FUN-680135 VARCHAR(1)
    l_exit_sw       LIKE type_file.chr1,                #Esc結束INPUT ARRAY 否 #No.FUN-680135 VARCHAR(1)
    p_cmd           LIKE type_file.chr1,                #處理狀態              #No.FUN-680135 VARCHAR(1)
    l_cnt           LIKE type_file.num5,                #No.FUN-680135 SMALLINT
    l_jump          LIKE type_file.num5,                #判斷是否跳過AFTER ROW的處理 #No.FUN-680135 SMALLINT
    l_allow_insert  LIKE type_file.num5,                #可新增否              #No.FUN-680135 SMALLINT
    l_allow_delete  LIKE type_file.num5                 #可刪除否              #No.FUN-680135 SMALLINT
 
    IF s_shut(0) THEN RETURN END IF
    LET g_gaz03=cl_get_progdesc(g_gam.gam01,g_lang) #TQC-740155
 
    CALL cl_opmsg('b')
    LET g_action_choice = ""
    LET l_allow_insert = cl_detail_input_auth("insert")
    LET l_allow_delete = cl_detail_input_auth("delete")
 
    LET g_forupd_sql = " SELECT gam01,gam02,gan01,gan03,ganmodu,gandate FROM gam_file,gan_file ",
                        "  WHERE gam01=gan02 AND gan02 = ? AND gan01= ? ",
                          " FOR UPDATE " 
 
    LET g_forupd_sql = cl_forupd_sql(g_forupd_sql)
    DECLARE gam_bcl CURSOR FROM g_forupd_sql    # LOCK CURSOR
 
    LET l_allow_insert = cl_detail_input_auth("insert")
    LET l_allow_delete = cl_detail_input_auth("delete")
    LET l_ac_t = 0
    WHILE TRUE
        LET l_exit_sw = "y"                #正常結束,除非 ^N
        INPUT ARRAY g_gan WITHOUT DEFAULTS FROM s_gan.*
 
      ATTRIBUTE(COUNT=g_rec_b,MAXCOUNT=g_max_rec,UNBUFFERED,
                INSERT ROW=l_allow_insert,DELETE ROW=l_allow_delete,APPEND ROW=l_allow_insert)

        BEFORE INPUT
          IF g_rec_b != 0 THEN
             CALL fgl_set_arr_curr(l_ac)
          END IF
 
        BEFORE ROW
            LET l_ac = ARR_CURR()
            DISPLAY l_ac TO FORMONLY.cn3  #ATTRIBUTE(RED)
            LET g_gan_t.* = g_gan[l_ac].*  #BACKUP
            LET l_n  = ARR_COUNT()
            BEGIN WORK
 
            OPEN zh_cl USING g_gam.gam01
            IF SQLCA.sqlcode THEN
               CLOSE zh_cl
               ROLLBACK WORK
            END IF
 
            FETCH zh_cl INTO g_gam.*            # 鎖住將被更改或取消的資料
            IF SQLCA.sqlcode THEN
               CLOSE zh_cl
               ROLLBACK WORK
            END IF
 
            IF g_gan_t.gan01 IS NOT NULL THEN
                LET p_cmd='u'
                OPEN gam_bcl USING g_gan[l_ac].gam01, g_gan[l_ac].gan01
                IF SQLCA.sqlcode THEN
                    CALL cl_err('OPEN gam_bcl',SQLCA.sqlcode,1)
                    LET l_lock_sw = "Y"
                END IF
 
                FETCH gam_bcl INTO g_gan[l_ac].*
                IF SQLCA.sqlcode THEN
                    CALL cl_err('FETCH gam_bcl',SQLCA.sqlcode,1)
                    LET l_lock_sw = "Y"
                END IF
            ELSE
                LET p_cmd='a'  #輸入新資料
                INITIALIZE g_gan[l_ac].* TO NULL
                LET g_gam.gamuser=g_user
                LET g_gam.gamgrup=g_grup
                LET g_gam.gammodu=g_grup
                LET g_gam.gamdate=g_today
                LET g_gam.gamoriu = g_user      
                LET g_gam.gamorig = g_grup 
                LET g_gan[l_ac].ganmodu = g_user
                LET g_gan[l_ac].gandate=TODAY
                DISPLAY g_gan[l_ac].* TO s_gan[l_sl].* ATTRIBUTE (YELLOW)
            END IF
            IF l_ac <= l_n then                  #DISPLAY NEWEST
                DISPLAY g_gan[l_ac].* TO s_gan[l_sl].* ATTRIBUTE (YELLOW)
            END IF
            CALL cl_show_fld_cont()     #FUN-550037(smin)
            #NEXT FIELD gan02
 
        BEFORE INSERT
            LET l_n = ARR_COUNT()
            LET p_cmd='a'
            INITIALIZE g_gan[l_ac].* TO NULL       
            LET g_gan_t.* = g_gan[l_ac].*           #新輸入資料
            LET g_gan[l_ac].ganmodu = g_user
            LET g_gan[l_ac].gandate=TODAY
            DISPLAY g_gan[l_ac].* TO s_gan[l_sl].* ATTRIBUTE (YELLOW)
            CALL cl_show_fld_cont()     #FUN-550037(smin)
            NEXT FIELD gam01

        AFTER FIELD gam01
            IF NOT cl_null(g_gan[l_ac].gam01) THEN
               SELECT gam02 INTO g_gan[l_ac].gam02
                 FROM gam_file
                WHERE gam01 = g_gan[l_ac].gam01
               LET g_gam.gam01 = g_gan[l_ac].gam01
               DISPLAY BY NAME g_gan[l_ac].gam02
            END IF
 
        AFTER FIELD gam02
            IF NOT cl_null(g_gan[l_ac].gam02) THEN
               LET  g_gam.gam02 = g_gan[l_ac].gam02
            END IF

        BEFORE FIELD gan01  #項次
            IF g_gan[l_ac].gan01 IS NULL OR g_gan[l_ac].gan01 = 0 THEN
                SELECT max(gan01)+1 INTO g_gan[l_ac].gan01
                   FROM gan_file WHERE gan02 = g_gan[l_ac].gam01
                IF g_gan[l_ac].gan01 IS NULL THEN
                    LET g_gan[l_ac].gan01 = 1
                END IF
                DISPLAY g_gan[l_ac].gan01 TO s_gan[l_sl].gan01
            END IF
            IF  g_gan[l_ac].gan01 IS NOT NULL THEN
              IF g_gan_t.gan01 IS NULL THEN  # 單身新增
                 LET g_gan[l_ac].ganmodu=g_user
                 LET g_gan[l_ac].gandate=TODAY
                 DISPLAY g_gan[l_ac].ganmodu TO s_gan[l_sl].ganmodu
                 DISPLAY g_gan[l_ac].gandate TO s_gan[l_sl].gandate
                 NEXT FIELD gan03
              END IF
            END IF
 
        AFTER FIELD gan03
            IF g_gan[l_ac].gan03 IS NULL THEN
               CALL cl_err('','aap-099',0)
               NEXT FIELD gan03
            END IF
            DISPLAY BY NAME g_gan[l_ac].gan03
      
        BEFORE DELETE                            #是否取消單身
            IF g_gan_t.gan01 IS NOT NULL THEN
                IF NOT cl_delb(0,0) THEN
                    LET l_exit_sw = "n"
                    LET l_ac_t = l_ac
                    EXIT INPUT
                END IF
                DELETE FROM gan_file
                    WHERE gan02 = g_gan[l_ac].gam01
                      AND gan01 = g_gan[l_ac].gan01
                IF SQLCA.sqlcode THEN
                    CALL cl_err(g_gan_t.gan01,SQLCA.sqlcode,0)
                    LET l_exit_sw = "n"
                    LET l_ac_t = l_ac
                    EXIT INPUT
                END IF
                    LET g_rec_b=g_rec_b-1
                    DISPLAY g_rec_b TO FORMONLY.cn2  #ATTRIBUTE(RED)
            #IF g_log='Y' THEN COMMIT WORK END IF
                COMMIT WORK
            END IF

        ON ROW CHANGE
           IF INT_FLAG THEN
               CALL cl_err('',9001,0)
               LET INT_FLAG = 0
               LET g_gan[l_ac].* = g_gan_t.*
               CLOSE gam_bcl
               ROLLBACK WORK
               EXIT INPUT
           END IF
           IF  l_lock_sw = 'Y' THEN
               CALL cl_err(g_gan[l_ac].gan01,-263,1)
               LET g_gan[l_ac].* = g_gan_t.*
           ELSE 
               UPDATE gan_file SET gan03 = g_gan[l_ac].gan03,
                                   gandate = g_gan[l_ac].gandate,
                                   ganmodu = g_gan[l_ac].ganmodu
                WHERE gan01 = g_gan[l_ac].gan01
                  AND gan02 = g_gan[l_ac].gam01
               IF SQLCA.sqlcode THEN
                 CALL cl_err3("upd","gan_file",g_gan[l_ac].gan01,g_gan[l_ac].gam01,SQLCA.sqlcode,"","",1) 
                 LET g_gan[l_ac].* = g_gan_t.*
                 ROLLBACK WORK
              ELSE
                 MESSAGE 'UPDATE O.K'
                 COMMIT WORK
              END IF
           END IF
 
        AFTER DELETE
          LET l_n = ARR_COUNT()
          INITIALIZE g_gan[l_n+1].* TO NULL
          LET l_jump = 1

        AFTER ROW
                LET l_ac = ARR_CURR()    #FUN-D30034 Add
                DISPLAY g_gan[l_ac].* TO s_gan[l_sl].* ATTRIBUTE (YELLOW) 
                IF INT_FLAG THEN                 #900423
                    CALL cl_err('',9001,0)
                    LET INT_FLAG = 0
                   #FUN-D30034--mark&add--str--
                   #LET g_gan[l_ac].* = g_gan_t.*
                   #DISPLAY g_gan[l_ac].* TO s_gan[l_sl].* ATTRIBUTE (YELLOW)
                    IF p_cmd = 'u' THEN
                       LET g_gan[l_ac].* = g_gan_t.*
                    ELSE
                       CALL g_gan.deleteElement(l_ac)
                       IF g_rec_b != 0 THEN
                          LET g_action_choice = "detail"
                          LET l_ac = l_ac_t
                       END IF
                    END IF
                   #FUN-D30034--mark&add--end--
                    EXIT INPUT
                END IF
 
                IF g_gan[l_ac].gan01 IS NULL THEN  #重要欄位空白,無效
                    INITIALIZE g_gan[l_ac].* TO NULL
                    DISPLAY g_gan[l_ac].* TO s_gan[l_sl].* ATTRIBUTE (YELLOW)
                END IF
 
                IF  g_gan[l_ac].gan01 IS NOT NULL THEN
                    IF g_gan_t.gan01 IS NULL THEN  # 單身新增
                       INSERT INTO gam_file VALUES (g_gam.*)
                       IF SQLCA.sqlcode THEN                           #置入資料庫不成功
                          #CALL cl_err3("ins","gam_file",g_gam.gam01,"",SQLCA.sqlcode,"","",1)    
                          UPDATE gam_file SET gamdate=g_today,gammodu=g_user WHERE gam01 = g_gam.gam01
                       END IF
                       INSERT INTO gan_file(gan01,gan02,gan03,ganmodu,gandate) #No.MOD-470041
                           VALUES(g_gan[l_ac].gan01,g_gam.gam01,
                                  g_gan[l_ac].gan03,g_gan[l_ac].ganmodu,
                                  g_gan[l_ac].gandate)
                        IF SQLCA.sqlcode THEN
                            CALL cl_err3("ins","gan_file",g_gam.gam01,g_gan[l_ac].gan01,SQLCA.sqlcode,"","",0)    #No.FUN-660081
                            LET g_gan[l_ac].* = g_gan_t.*
                            DISPLAY g_gan[l_ac].* TO s_gan[l_sl].* ATTRIBUTE (YELLOW)
                        ELSE
                            MESSAGE 'INSERT O.K'
                                LET g_rec_b=g_rec_b+1
                                DISPLAY g_rec_b TO FORMONLY.cn2  #ATTRIBUTE(RED)
                            COMMIT WORK
                        END IF
                    END IF
                END IF
 
                LET l_ac_t = l_ac   #FUN-D30034 Add 
               #LET g_gan_t.* = g_gan[l_ac].*          # 900423    #FUN-D30034 Mark
 
 
        ON KEY(CONTROL-N)
            CALL zh_b_askkey()
            LET l_exit_sw = "n"
            EXIT INPUT
 
        ON KEY(CONTROL-O)                        #沿用所有欄位
            IF INFIELD(gan01) AND l_ac > 1 THEN
                LET g_gan[l_ac].* = g_gan[l_ac-1].*
                #LET g_gan[l_ac].gan03 = l_ac
                DISPLAY g_gan[l_ac].* TO s_gan[l_sl].* ATTRIBUTE (YELLOW)
                NEXT FIELD gan01
            END IF
 
        ON KEY(CONTROL-G)
            CALL cl_cmdask()
        ON ACTION controls                             #No.FUN-6A0092
         CALL cl_set_head_visible("","AUTO")           #No.FUN-6A0092
 
        ON KEY(CONTROL-F)
            CASE
                WHEN INFIELD(gan02) CALL cl_fldhlp('gan02')
                WHEN INFIELD(gan01) CALL cl_fldhlp('gan01')
                WHEN INFIELD(gan05) CALL cl_fldhlp('gan05')
                OTHERWISE          CALL cl_fldhlp('    ')
            END CASE
#TQC-860017 start
 
        ON ACTION about
           CALL cl_about()
 
        ON ACTION help
           CALL cl_show_help()
 
        ON IDLE g_idle_seconds
           CALL cl_on_idle()
           CONTINUE INPUT
#TQC-860017 end
 
        END INPUT
 
 
        IF l_exit_sw = "y" THEN
            EXIT WHILE                     #ESC 或 DEL 結束 INPUT
        ELSE
            CONTINUE WHILE                 #^N 結束 INPUT
        END IF
    END WHILE
 
    CLOSE gam_bcl
    #IF g_log = 'Y' THEN COMMIT WORK END IF
    COMMIT WORK
END FUNCTION
 
FUNCTION zh_u()
    IF s_shut(0) THEN RETURN END IF
    IF g_gam.gam01 IS NULL THEN
        CALL cl_err('',-400,0)
        RETURN
    END IF
    SELECT gam01 INTO g_gam.gam01 FROM gam_file
     WHERE gam01=g_gam.gam01
    CALL cl_opmsg('u')
    LET g_gam01_t = g_gam.gam01
    BEGIN WORK
 
    OPEN zh_cl USING g_gam.gam01
    IF SQLCA.sqlcode THEN
        CALL cl_err('FETCH z_cl',SQLCA.sqlcode,1)      # 資料被他人LOCK
        RETURN
    END IF
 
    FETCH zh_cl INTO g_gam.*            # 鎖住將被更改或取消的資料
    IF SQLCA.sqlcode THEN
        CALL cl_err('FETCH z_cl',SQLCA.sqlcode,1)      # 資料被他人LOCK
        RETURN
    END IF
 
    CALL zh_show()
    WHILE TRUE
        LET g_gam01_t = g_gam.gam01
        LET g_gam_o.* = g_gam.*
        #LET g_gam.gammodu=g_user
        #LET g_gam.gamdate=g_today
        CALL zh_i("u")                      #欄位更改
        IF INT_FLAG THEN
            LET INT_FLAG = 0
            LET g_gam.*=g_gam_t.*
            CALL zh_show()
            CALL cl_err('','9001',0)
            EXIT WHILE
        END IF
        UPDATE gam_file SET gam02   = g_gam.gam02,
                            gammodu = g_user,
                            gamdate = today
            WHERE gam01 = g_gam.gam01
        IF SQLCA.sqlcode OR SQLCA.sqlerrd[3] = 0 THEN
            #CALL cl_err(g_gam.gam02,SQLCA.sqlcode,0)  #No.FUN-660081
            CALL cl_err3("upd","gam_file",g_gam01_t,"",SQLCA.sqlcode,"","",0)    #No.FUN-660081
            CONTINUE WHILE
        END IF
        EXIT WHILE
    END WHILE
    CLOSE zh_cl
    IF g_log = 'Y' THEN COMMIT WORK END IF
END FUNCTION
 
FUNCTION zh_b_askkey()
DEFINE
    l_wc2           LIKE type_file.chr1000  #No.FUN-680135 VARCHAR(200)
 
 CONSTRUCT l_wc2 ON gan02,gan01,gan05
            FROM s_gan[1].gan02,s_gan[1].gan01,s_gan[1].gan05
#TQC-860017 start
       ON ACTION about
          CALL cl_about()
 
       ON ACTION controlg
          CALL cl_cmdask()
 
       ON ACTION help
          CALL cl_show_help()
 
       ON IDLE g_idle_seconds
          CALL cl_on_idle()
          CONTINUE CONSTRUCT
    END CONSTRUCT
#TQC-860017 end
    IF INT_FLAG THEN
        LET INT_FLAG = 0
        RETURN
    END IF
    CALL zh_b_fill(l_wc2)
END FUNCTION
 
FUNCTION zh_b_fill(p_wc2)                   #BODY FILL UP
DEFINE
    p_wc2           LIKE type_file.chr1000, #No.FUN-680135 VARCHAR(200)
    num_t           LIKE type_file.num10    #No.FUN-680135 INTEGER

    LET g_sql =
        "SELECT gam01,gam02,gan01,gan03,ganmodu,gandate,gan03 ",
        " FROM gan_file,gam_file ",
        " WHERE gan02 = gam01 ",
        "   AND " , g_wc CLIPPED,
        " ORDER BY 1"
 
    PREPARE gam_pb FROM g_sql
    DECLARE gan_curs                       #SCROLL CURSOR
        CURSOR FOR gam_pb
 
    CALL g_gan.clear()
 
    LET g_rec_b = 0
    LET g_cnt = 1
    LET num_t=0
    FOREACH gan_curs INTO g_gan[g_cnt].*   #單身 ARRAY 填充
        LET g_rec_b = g_rec_b + 1
        LET num_t=num_t + 1
        IF SQLCA.sqlcode THEN
            CALL cl_err('foreach:',SQLCA.sqlcode,1)
            EXIT FOREACH
        END IF
        LET g_cnt = g_cnt + 1
        IF g_cnt > g_max_rec THEN
            CALL cl_err('',9035,0)
            EXIT FOREACH
        END IF
    END FOREACH
    CALL g_gan.deleteElement(g_cnt)       #MOD-EC0080
    LET g_rec_b = g_cnt-1                 #MOD-EC0080
    CALL SET_COUNT(g_cnt-1)               #告訴I.單身筆數
    #IF g_query='Y' THEN                  #顯示合乎條件筆數
        DISPLAY g_rec_b TO FORMONLY.cn2   #ATTRIBUTE(RED)
        #LET g_cnt = 0
        #DISPLAY g_cnt   TO FORMONLY.cnt  #ATTRIBUTE(RED)
    #END IF
END FUNCTION
 
FUNCTION zh_bp(p_ud)
 
   DEFINE p_ud     LIKE type_file.chr1    #No.FUN-680135 VARCHAR(1)
 
   IF p_ud<>'G' OR g_action_choice = "detail" THEN
      RETURN
   END IF
 
   LET g_action_choice = " "
   CALL cl_set_act_visible("accept,cancel", FALSE)
   DISPLAY ARRAY g_gan TO s_gan.* ATTRIBUTE(COUNT=g_rec_b,UNBUFFERED)
 
      BEFORE ROW
         LET l_ac = ARR_CURR()
      CALL cl_show_fld_cont()            #No.FUN-550037 hmf
 
 
      ON ACTION query
         LET g_action_choice="query"
         EXIT DISPLAY                    # Q.查詢
 
      ON ACTION exporttoexcel
         LET g_action_choice = 'exporttoexcel'
         EXIT DISPLAY

      ON ACTION detail
         LET g_action_choice="detail"
         LET l_ac =1
         EXIT DISPLAY                    # B.單身

      ON ACTION related_document
         LET g_action_choice="related_document"
         EXIT DISPLAY
 
      ON ACTION help
         LET g_action_choice="help"
         EXIT DISPLAY                    # H.說明
 
      ON ACTION locale
         CALL cl_dynamic_locale()
          CALL cl_show_fld_cont()                   #No.FUN-550037 hmf
         EXIT DISPLAY
 
      ON ACTION exit
         LET g_action_choice="exit"
         EXIT DISPLAY                    # Esc.結束
 
      ON ACTION controlg
         LET g_action_choice="controlg"
         EXIT DISPLAY
      ON ACTION controls                             #No.FUN-6A0092
         CALL cl_set_head_visible("","AUTO")           #No.FUN-6A0092
 
      ON ACTION accept
         LET g_action_choice="detail"
         LET l_ac = ARR_CURR()
         EXIT DISPLAY
 
      ON ACTION cancel
             LET INT_FLAG=FALSE 		#MOD-570244	mars
         LET g_action_choice="exit"
         EXIT DISPLAY
 
      ON IDLE g_idle_seconds
         CALL cl_on_idle()
         CONTINUE DISPLAY
 
      # No.FUN-530067 --start--
      AFTER DISPLAY
         CONTINUE DISPLAY
      # No.FUN-530067 ---end---
 
 
   END DISPLAY
   CALL cl_set_act_visible("accept,cancel", TRUE)
END FUNCTION
 
 
 
